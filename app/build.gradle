apply plugin: 'com.android.application'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply from: '../config/config-android.gradle'
apply plugin: Plugins.navigationSafeArgsKotlin
apply from: '../config/config-ui-android.gradle'
apply from: '../config/dependency-groups.gradle'

android {
    defaultConfig {
        applicationId "com.averin.android.developer"
        buildConfigField 'String', 'APP_CENTER_SECRET', '"b173908e-10b5-47c0-83bb-88769d82b028"'
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "2048M"
    }


    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    signingConfigs {
        debug {
            storeFile file('debug.jks')
        }
        release {
//            storeFile file('MY_KEY_STORE_KEY_STORE.jks')
//            storePassword KEYSTORE_PASSWORD
//            keyAlias KEYALIAS
//            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            matchingFallbacks = ['release', 'debug']
        }
    }

    flavorDimensions "distribute"
    productFlavors {
        dev {
            buildConfigField 'String', 'API_BASE_URL', '"https://api.github.com/"'
            dimension "distribute"
            versionNameSuffix '-dev'
            applicationIdSuffix '.dev'
        }
        prod {
            buildConfigField 'String', 'API_BASE_URL', '"https://api.github.com/"'
            dimension "distribute"
            versionNameSuffix ''
            applicationIdSuffix ''
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
    testOptions {
        unitTests.all { jvmArgs '-noverify' }
    }
    aaptOptions {
        cruncherEnabled = false
    }
}

dependencies {
    implementation project(Modules.core)
    implementation project(Modules.features)
    dependencyAndroidXMultidex()
    dependencyMiscDateTime()
    dependencyKotlinAll()
    dependencyDiMain()
    dependencyDiTest()
    dependencyAndroidXUi()
    dependencyAndroidXNavigation()
    dependencyNetworkAll()
    dependencyMiscDelegates()
    dependencyJsonMoshi()
    dependencyMiscTimber()
    dependencyTestsAll()
    dependencyGlide()
}